{-# OPTIONS --safe --cubical #-}

module Weakening.Presheaf.Cartesian where

open import Agda.Primitive
open import Library.Equality
open import Library.Sets
open import Library.Pairs public
open import Library.Pairs.Sets
open import Weakening.Variable.Base
open import Weakening.Presheaf
open import Weakening.Presheaf.Category


private
  variable
    l m n k : Level
    F : Pshw {l}
    G : Pshw {m}
    H : Pshw {n}
    K : Pshw {k}


-- Cartesian structure of the category of presheaves.
-- Terminal object.
ğŸ™p : âˆ€ {l} â†’ Pshw {l}
(ğŸ™p {l}) $o Î“ = âŠ¤l {l}
isSetapply ğŸ™p = PropisSet isPropâŠ¤l
x +âŸ¨ ğŸ™p âŸ© Ïƒ = x
+id ğŸ™p = refl
+âˆ˜ ğŸ™p = refl

!p : âˆ€ {l m} {F : Pshw {l}} â†’ Natw F (ğŸ™p {m})
act !p Î“ x = tt
nat !p = refl

!pÎ· : âˆ€ {l m} {F : Pshw {l}} {Î¸ : Natw F (ğŸ™p {m})} â†’ Î¸ â‰¡ !p
!pÎ· = natâ‰¡ Î» i Î“ x â†’ tt

-- Products
infixl 10 _Ã—p_ _,n_ _Ã—n_

_Ã—p_ : âˆ€ {l m} â†’ Pshw {l} â†’ Pshw {m} â†’ Pshw
(F Ã—p G) $o Î“ = F $o Î“ Ã— G $o Î“
isSetapply (F Ã—p G) = isSetÃ— (isSetapply F) (isSetapply G)
(x ,, y) +âŸ¨ F Ã—p G âŸ© Ïƒ = x +âŸ¨ F âŸ© Ïƒ ,, y +âŸ¨ G âŸ© Ïƒ
+id (F Ã—p G) {x = x ,, y} i = +id F {x = x} i ,, +id G {x = y} i
+âˆ˜ (F Ã—p G) {x = x ,, y} {Ïƒ} {Î½} i =
  +âˆ˜ F {x = x} {Ïƒ} {Î½} i ,, +âˆ˜ G {x = y} {Ïƒ} {Î½} i

-- Projections
-- Note that the two presheaves whose product is used must can not be
-- inferred in general.
Ï€â‚n : {F : Pshw {l}} (G : Pshw {m}) (H : Pshw {n}) â†’ Natw F (G Ã—p H) â†’ Natw F G
act (Ï€â‚n G H Î¸) Î“ x = fst (act Î¸ Î“ x)
nat (Ï€â‚n G H Î¸) = ap fst (nat Î¸)

Ï€â‚‚n : {F : Pshw {l}} (G : Pshw {m}) (H : Pshw {n}) â†’ Natw F (G Ã—p H) â†’ Natw F H
act (Ï€â‚‚n G H Î¸) Î“ x = snd (act Î¸ Î“ x)
nat (Ï€â‚‚n G H Î¸) = ap snd (nat Î¸)

_,n_ : Natw F G â†’ Natw F H â†’ Natw F (G Ã—p H)
act (Î¸ ,n Î±) Î“ x = act Î¸ Î“ x ,, act Î± Î“ x
nat (Î¸ ,n Î±) = ap2 _,,_ (nat Î¸) (nat Î±)

-- Laws
Ï€â‚nÎ² : {F : Pshw {l}} {G : Pshw {m}} {H : Pshw {n}} {Î¸ : Natw F G} {Î± : Natw F H} â†’
       Ï€â‚n G H (Î¸ ,n Î±) â‰¡ Î¸
Ï€â‚nÎ² = natâ‰¡ refl

Ï€â‚‚nÎ² : {F : Pshw {l}} {G : Pshw {m}} {H : Pshw {n}} {Î¸ : Natw F G} {Î± : Natw F H} â†’
       Ï€â‚‚n G H (Î¸ ,n Î±) â‰¡ Î±
Ï€â‚‚nÎ² = natâ‰¡ refl

Ï€nÎ· : {F : Pshw {l}} {G : Pshw {m}} {H : Pshw {n}} {Î¸ : Natw F (G Ã—p H)} â†’
      (Ï€â‚n G H Î¸ ,n Ï€â‚‚n G H Î¸) â‰¡ Î¸
Ï€nÎ· {Î¸ = Î¸} = natâ‰¡ (Î» i Î“ x â†’ act Î¸ Î“ x)

,âˆ˜n : {F : Pshw {l}} {G : Pshw {m}} {H : Pshw {n}} {K : Pshw {k}}
      {Î¸ : Natw F G} {Î± : Natw F H} {Î² : Natw K F} â†’
      (Î¸ ,n Î±) âˆ˜n Î² â‰¡ (Î¸ âˆ˜n Î² ,n Î± âˆ˜n Î²)
,âˆ˜n = natâ‰¡ refl


_Ã—n_ : Natw F G â†’ Natw H K â†’ Natw (F Ã—p H) (G Ã—p K)
_Ã—n_ {F = F} {G = G} {H = H} {K = K} Î¸ Î± =
  (Î¸ âˆ˜n (Ï€â‚n F H idn)) ,n (Î± âˆ˜n (Ï€â‚‚n F H idn))
